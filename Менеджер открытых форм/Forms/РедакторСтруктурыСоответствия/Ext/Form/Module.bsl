
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	
	ТипКоллекцииКорня = ТипКоллекции(Параметры.РедактируемоеЗначение);
	
	СписокВыбора = Элементы.РедактируемоеЗначениеТипКоллекции.СписокВыбора;
	СписокВыбора.Добавить("", "");
	Для Каждого ДоступныйТип Из ДоступныеТипыКоллекций() Цикл
		СписокВыбора.Добавить(ДоступныйТип, ДоступныйТип);
	КонецЦикла;
	
	ОписаниеКорня = РедактируемоеЗначение.ПолучитьЭлементы().Добавить();
	ОписаниеКорня.ТипКоллекции = ТипКоллекции(Параметры.РедактируемоеЗначение);
	
	ЗаполнитьДеревоИзСтруктурыСоответствия(Параметры.РедактируемоеЗначение, ОписаниеКорня);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КорневыеЭлементы = РедактируемоеЗначение.ПолучитьЭлементы();
	Если ЗначениеЗаполнено(КорневыеЭлементы) Тогда
		Элементы.РедактируемоеЗначение.Развернуть(КорневыеЭлементы[0].ПолучитьИдентификатор(), Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура РедактируемоеЗначениеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Отказ = Истина;
	Иначе
		ТекущийРодитель = ТекущиеДанные.ПолучитьРодителя();
		Если Копирование И ТекущийРодитель = Неопределено Тогда
			Отказ = Истина;
		ИначеЕсли ПустаяСтрока(ТекущиеДанные.ТипКоллекции) Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура РедактируемоеЗначениеПриАктивизацииСтроки(Элемент)
	
    ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Родитель = ТекущиеДанные.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрЗаканчиваетсяНа(Родитель.ТипКоллекции, "Структура") Тогда
		Элементы.РедактируемоеЗначениеКлюч.ВыбиратьТип = Ложь;
		ТекущиеДанные.Ключ = Строка(ТекущиеДанные.Ключ);
	Иначе
		Элементы.РедактируемоеЗначениеКлюч.ВыбиратьТип = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	
	ОписаниеКорня      = РедактируемоеЗначение.ПолучитьЭлементы()[0];
	
	ПолученноеЗначение = НоваяКоллекция(ОписаниеКорня.ТипКоллекции);
	
	ЗаполнитьРезультат(ОписаниеКорня, ПолученноеЗначение);
	
	ПолученноеЗначение = ЗапечататьФиксированнуюКоллекцию(ПолученноеЗначение, ОписаниеКорня.ТипКоллекции);
	
	ЭтаФорма.Закрыть(Новый Структура("ЗначениеРеквизита", ПолученноеЗначение));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьРезультат(ИсходнаяКоллеция, ЗаполняемоеЗначение)
	
	Для Каждого ОписаниеЗначения Из ИсходнаяКоллеция.ПолучитьЭлементы() Цикл
		
		Если ЗначениеЗаполнено(ОписаниеЗначения.ТипКоллекции) Тогда
			ТекущееЗначение = НоваяКоллекция(ОписаниеЗначения.ТипКоллекции);
			ЗаполнитьРезультат(ОписаниеЗначения, ТекущееЗначение);
			ТекущееЗначение = ЗапечататьФиксированнуюКоллекцию(ТекущееЗначение, ОписаниеЗначения.ТипКоллекции);
		Иначе
			ТекущееЗначение = ОписаниеЗначения.Значение;
		КонецЕсли;
		
		ЗаполняемоеЗначение.Вставить(ОписаниеЗначения.Ключ, ТекущееЗначение);
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста	
Функция ТипКоллекции(Коллекция) Экспорт
	
	ТипКоллекции = ТипЗнч(Коллекция);
	
	Для Каждого ИмяТипа Из ДоступныеТипыКоллекций() Цикл
		Если ТипКоллекции = Тип(ИмяТипа) Тогда
			Возврат ИмяТипа;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции
	
&НаКлиентеНаСервереБезКонтекста
Функция ДоступныеТипыКоллекций()
	
	Возврат СтрРазделить("Структура,Соответствие,ФиксированнаяСтруктура,ФиксированноеСоответствие", ",");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НоваяКоллекция(ТипКоллекции)
	
	Если СтрЗаканчиваетсяНа(ТипКоллекции, "Структура") Тогда
		Возврат Новый Структура;
	Иначе
		Возврат Новый Соответствие;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗапечататьФиксированнуюКоллекцию(Коллекция, ТипКоллекции)
	
	Если НЕ СтрНачинаетсяС(ТипКоллекции, "Фикс") Тогда
		Возврат Коллекция;
	ИначеЕсли ТипЗнч(Коллекция) = Тип("Структура") Тогда
		Возврат Новый ФиксированнаяСтруктура(Коллекция);
	Иначе
		Возврат Новый ФиксированноеСоответствие(Коллекция);
	КонецЕсли;
	
КонецФункции
 
&НаСервере
Процедура ЗаполнитьДеревоИзСтруктурыСоответствия(ТекущееЗначение, ТекущийРодитель)
	
	КоллекцияСтрок = ТекущийРодитель.ПолучитьЭлементы();
	Для Каждого КлючИЗначение Из ТекущееЗначение Цикл
		
		ОписаниеСтроки = КоллекцияСтрок.Добавить();
		ОписаниеСтроки.Ключ = КлючИЗначение.Ключ;
		
		ОписаниеСтроки.ТипКоллекции = ТипКоллекции(КлючИЗначение.Значение);
		
		Если ЗначениеЗаполнено(ОписаниеСтроки.ТипКоллекции) Тогда
			ЗаполнитьДеревоИзСтруктурыСоответствия(КлючИЗначение.Значение, ОписаниеСтроки);
		Иначе
			ОписаниеСтроки.Значение = КлючИЗначение.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоСтруктура(Значение)
	
	ТипТекЗнч = ТипЗнч(Значение);
	Возврат ТипТекЗнч = Тип("Структура") ИЛИ ТипТекЗнч = Тип("ФиксированнаяСтруктура");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоСоответствие(Значение)
	
	ТипТекЗнч = ТипЗнч(Значение);
	Возврат ТипТекЗнч = Тип("Соответствие") ИЛИ ТипТекЗнч = Тип("ФиксированноеСоответствие");
	
КонецФункции

#КонецОбласти
